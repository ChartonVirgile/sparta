################################################################################
# Flow over flat plate 3D
################################################################################

seed    		50791
dimension		3

global                  gridcut 1.0e-7
global                  comm/sort yes

##########
# Domain #
##########

variable xl equal -0.00015692
variable yl equal -0.00225692
variable zl equal -0.00225692

# about 50 mfp
variable xh equal 0.0015692
variable yh equal 0.00225692
variable zh equal 0.00225692

variable Nx equal 110
variable Ny equal 300
variable Nz equal 300

boundary		o s s

create_box		${xl} ${xh} ${yl} ${yh} ${zl} ${zh}
create_grid		${Nx} ${Ny} ${Nz}
balance_grid		rcb cell

##################
# Gas properties #
##################
variable p equal 1000
variable Tinf equal 300.0
variable kb equal 1.38e-23
variable nden equal ${p}/${kb}/${Tinf}

variable Ma equal 0.2
variable RO2 equal 259.8
variable gamma equal 1.4
variable c equal sqrt(${gamma}*${RO2}*${Tinf})
variable Uinf equal ${c}*${Ma}
print "  speed = ${Uinf}"

surf_collide        positive diffuse ${Tinf} 1.0 translate ${Uinf} 0.0 0.0
surf_collide        negative diffuse ${Tinf} 1.0 translate ${Uinf} 0.0 0.0
bound_modify        yhi collide positive
bound_modify        ylo collide negative 
bound_modify        zhi collide positive
bound_modify        zlo collide negative  

species air.species O2 CO2
mixture air vstream ${Uinf} 0.0 0.0 temp ${Tinf} nrho ${nden}
mixture air O2 frac 1.0
mixture air CO2 frac 0.0

variable mu equal 4.912e-5 #o2 viscosity at 1000K
variable m equal 5.31e-26
variable mfp equal ${mu}/${p}*sqrt(3.14159*${kb}*${Tinf}*0.5/${m})
print "  mfp = ${mfp}"
variable Lxomfp equal (${xh}-${xl})/${mfp}
print "  Lx/mfp = ${Lxomfp}"
variable Lyomfp equal (${yh}-${yl})/${mfp}
print "  Ly/mfp = ${Lyomfp}"
variable vth equal sqrt(2*${kb}*${Tinf}/3.14159/${m})
print "  mean thermal speed = ${vth}"

variable Re equal ${nden}*${m}*${Uinf}*${xh}/${mu}
print "  Re = ${Re}"
variable blth equal 5.0*${xh}/sqrt(${Re})
print "  Boundary Layer Thickness at end = ${blth}"

###########
# Collide #
###########
collide             vss all air.vss
collide_modify      remain no
collide_modify      vremax 0 no

######################
# Surface conditions #
######################
global surfs explicit/distributed
read_surf data.flat

variable surftemp equal ${Tinf}
surf_react 1 prob flat.surf
surf_collide 1 diffuse ${surftemp} 1.0
surf_modify all collide 1 react 1

compute 1s react/isurf/grid all 1 r:O2 p:CO2
fix 1s ave/grid all 1 20 20 c_1s[*]

variable scale_ablate equal 0.005

fix fablate ablate all 20 ${scale_ablate} f_1s[1] distributed no
create_isurf all fablate 100.5 inner buffer 0.005

#######################
# Boundary conditions #
#######################
fix inflow emit/face air xlo

variable ppc equal 40
variable vol equal (${xh}-${xl})*(${yh}-${yl})*(${zh}-${zl})
variable Fnum equal ${nden}*${vol}/${ppc}/${Nx}/${Ny}/${Nz}
print "  fnum equal ${Fnum}"

global      nrho ${nden} temp ${Tinf} fnum ${Fnum}

create_particles air n 0

variable tc equal ${mfp}/${vth}
print "  mean collision time = ${tc}"
variable dt equal 1.0e-7
variable xadvect equal ${Uinf}*${dt}
print "  mean advection distance in one dt = ${xadvect}"

##################
# Run and Output #
##################

timestep ${dt}

compute 1d grid all all u v w
compute 2d thermal/grid all all temp

fix 1d ave/grid all 5 500 2500 c_1d[*] c_2d[*] ave one

dump 1o grid all 100000 grid_cone_new.* xc yc zc f_1d[*]

compute 3d property/grid all vol
compute 3dr reduce sum c_3d[*]

variable vout equal c_3dr
fix massr print 1000 "${vout}" append vol.dat screen no

dump 1 image all 10000 snap.*.ppm type type surf one 0.02 particle no box yes 0.002 gline no 0.003 view 30 60

stats			1000
stats_style		step np nattempt ncoll nscoll

write_surf rough.0.surf
run 100000
write_surf rough.1.surf
run 100000
write_surf rough.2.surf
run 100000
write_surf rough.3.surf
run 100000
write_surf rough.4.surf
run 100000
write_surf rough.5.surf
run 100000
write_surf rough.6.surf
run 100000
write_surf rough.7.surf
run 100000
write_surf rough.8.surf




